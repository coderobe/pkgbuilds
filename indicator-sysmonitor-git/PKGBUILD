# Maintainer: Robin Broda <robin at broda dot me>

_pkgbase=indicator-sysmonitor
pkgbase="${_pkgbase}-git"
pkgname=("${_pkgbase}-budgie-git" "${_pkgbase}-appindicator-git")
pkgver=r90.f5f5f6f
pkgrel=6
pkgdesc='A configurable system monitoring applet'
arch=('any')
url='https://github.com/fossfreedom/indicator-sysmonitor'
license=('GPL3')
conflicts=('indicator-sysmonitor')
provides=('indicator-sysmonitor')
depends=('python' 'python-psutil')
makedepends=('git' 'libappindicator-gtk3')
source=('git://github.com/fossfreedom/indicator-sysmonitor.git'
        'symlink.patch')
md5sums=('SKIP'
         'SKIP')

pkgver() {
  cd "${_pkgbase}"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd "${_pkgbase}"

  # Fix symlink path
  patch Makefile "${srcdir}/symlink.patch"

  # Fix usage of DESTDIR
  sed -i -E 's/(DESTDIR)/_\1/gm' Makefile
  sed -i -E 's/^_(DESTDIR)=([^ ])/_\1=\${\1}\2/' Makefile

  # Fix for path expansion with vars containing spaces
  sed -i -E 's/([^ =]*\$\{[^\}]+\}[^ ]*)/"\1"/gm' Makefile
}

package_indicator-sysmonitor-budgie-git() {
  cd "${_pkgbase}"
  make DESTDIR="${pkgdir}" installbudgie
}

package_indicator-sysmonitor-appindicator-git() {
  depends+=('libappindicator-gtk3')
  cd "${_pkgbase}"
  make DESTDIR="${pkgdir}" install
}
